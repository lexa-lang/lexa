import immutable/dequeue
import immutable/option
import text/string
import io/args

interface Process {
  def yield(): Unit
  def exit(): Nothing
  def fork { p: () => Unit / Process }: Unit
}

extern pure def cast[T, R](x : T): R =
  "${x}"


def scheduler { prog: => Unit / Process } = region this {
  var queue: Dequeue[() => Unit at {this, prog}] in this = emptyQueue();

  def run(): Unit = queue.popBack match {
    case None() => ()
    case Some((k, q)) =>
      queue = q
      k();
      run()
  }

  def runnext(): Unit = queue.popFront match {
    case None() => ()
    case Some((k, q)) =>
      queue = q
      k()
  }

  def spawn(prog1: () => Unit / Process at {this, prog} ): Unit = {
    try { prog1() } with Process {
      def yield() = {
        queue = queue.pushFront(fun() { resume(()) })
        runnext()
      }
      def fork() = {
        queue = queue.pushFront(fun() { resume { {_: () => Unit / Process } => () } })
        resume { {p: () => Unit / Process } => spawn(cast(box p)) } 
      }
      def exit() = ()
    }
    runnext()
  }

  spawn(prog)
}

def run(n: Int): Int = {
  var c = 0;
  def job(): Unit / Process = {
    do yield();
    c = c + 1;
    do yield();
    c = c + 1;
  }
  scheduler {
    def loop(i: Int): Unit = {
        if (i < n) {
            do fork {job}
            loop(i + 1)
        }
    };
    loop(0)
  };
  c
}

def main() = commandLineArgs() match {
  case Nil() => println(run(1000000))
  case Cons(x, Nil()) => toInt(x) match {
    case Some(i) => println(run(i))
    case _ => println("Unexpected non-integer(s) '" ++ show(x) ++ "'")
  }
  case other => println("Expects zero or one argument, not '" ++ show(size(other)) ++ "'")
}