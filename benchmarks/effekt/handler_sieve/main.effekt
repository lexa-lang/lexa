import args
import bench

effect Prime(e: Int): Bool

def primes(i: Int, n: Int, a: Int): Int / {Prime} =
  if (i >= n) {
    a
  } else {
    if (do Prime(i)) {
      try {
        primes(i + 1, n, a + i)
      } with Prime { (e: Int) =>
        if (mod(e, i) == 0) {
          resume(false)
        } else {
          resume(do Prime(e))
        }
      }
    } else {
      primes(i + 1, n, a)
    }
  }

def run(n: Int) = {
  try {
    primes(2, n, 0)
  } with Prime { (e: Int) =>
    resume(true)
  }
}

def main() = ignore[WrongFormat] {commandLineArgs() match {
  case Nil() => println("Expects one argument")
  case Cons(x, Cons(y, Nil())) => 
    println("Measuring internal running time")
    val i = x.toInt; var r = 0; val t = timed{ r = run(i); };
    println("Nanosecond used: " ++ show(t))
    println(r)
  case Cons(x, Nil()) =>
    val i = x.toInt; val r = run(i);
    println(r)
  case other => println("Expects one argument, not '" ++ show(size(other)) ++ "'")
}
}
